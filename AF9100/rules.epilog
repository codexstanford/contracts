%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% basics
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

claim.recommendation(Claim, pay) :-
  plan_in_effect(Claim) &
  covered(Claim, Reason) &
  ~excluded(Claim)

excluded(C) :-
  exclusion(C, E)

plan_in_effect(C) :-
  claim.policy(C,P) &
  claim.hospitalization(C,Z) &
  policy.startdate(P,PS) &
  policy.enddate(P,PE) &
  hospitalization.startdate(Z,ZS) &
  hospitalization.enddate(Z,ZE) &
  overlap(PS,PE,ZS,ZE)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% coverage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

covered(C, confinement) :-
  claim.hospitalization(Claim, Hosp) &
  hospitalization.facility(Hosp, hospital) &
  hospitalization.cause(Hosp, Cause) &
  cause.covered(Cause) &
  duration(Hosp, Duration) &
  greater(Duration, 82800000)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% exclusions
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

exclusion(C, 2_a_1) :- claim.exclusion(C, preexist)
exclusion(C, 2_b) :- claim.exclusion(C, sanctions)
exclusion(C, 2_c) :- claim.exclusion(C, fraud)

% Section D: Excluded causes

exclusion(C, Section) :-
  claim.hospitalization(C, Z) &
  hospitalization.cause(Z, Cause) &
  excluded_cause(Cause, Section)

% Routine pregnancy/newborn care in the first 10 months of coverage
exclusion(C, 2_d_1) :-
  claim.hospitalization(C,Z) &
  hospitalization.cause(Z, natal) &
  ~hospitalization.cause(Z, pregnancy_complication) &
  claim.policy(C,P) &
  policy.startdate(P,PS) &
  parsedate(PS, StartDate) &
  hospitalization.startdate(Z,ZS) &
  parsedate(ZS, HospDate) &
  months_since(StartDate, HospDate, Months) &
  less(Months, 10)

excluded_cause(routine_newborn, 2_d_2)
excluded_cause(drugs, 2_d_3)
excluded_cause(felony, 2_d_4)
excluded_cause(intoxicated, 2_d_5)
excluded_cause(self_inflicted, 2_d_6)
excluded_cause(dental_non_accident, 2_d_7)
excluded_cause(surgery_not_necessary, 2_d_8)
excluded_cause(war, 2_d_9)

% Organ donation in the first year of coverage
exclusion(C, 2_d_10) :-
  claim.hospitalization(C,Z) &
  hospitalization.cause(Z, organ_donation) &
  claim.policy(C,P) &
  policy.startdate(P,PS) &
  parsedate(PS, StartDate) &
  hospitalization.startdate(Z,ZS) &
  parsedate(ZS, HospDate) &
  months_since(StartDate, HospDate, Months) &
  less(Months, 12)

exclusion(C, 2_d_11) :-
  claim.hospitalization(C, Z) &
  hospitalization.cause(Z, mental) &
  ~hospitalization.cause(Z, senility)