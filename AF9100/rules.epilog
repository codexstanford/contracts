%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% basics
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

covers(af9100, Hospitalization) :-
  covered(af9100, Hospitalization, Reason) &
  ~excluded(af9100, Hospitalization)

excluded(af9100, Hospitalization) :-
  exclusion(af9100, Hospitalization, E)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% coverage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Cover hospital confinement by default, but many exceptions coming
covered(af9100, Hospitalization, confinement) :-
  hospitalization.facility(Hospitalization, hospital)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% exclusions
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

exclusion(af9100, Hospitalization, not_in_effect) :-
  hospitalization.startdate(Hospitalization, Start) &
  hospitalization.enddate(Hospitalization, End) &
  ~policy_in_effect(af9100, Start, End)

exclusion(af9100, Hospitalization, short) :-
  duration(Hospitalization, Duration) &
  less(Duration, 82800000)

exclusion(af9100, Hospitalization, Section) :-
  eventual_cause_type(Hospitalization, Type) &
  excluded_cause(Type, Section)

% Care for pre-existing condition in first 12 months of coverage
exclusion(af9100, Hospitalization, 2_a_1) :-
  eventual_cause_type(Hospitalization, preexist) &
  policy.startdate(lfsc, PS) &
  evaluate(parsedate(PS), StartDate) &
  hospitalization.startdate(Hospitalization, ZS) &
  evaluate(parsedate(ZS), HospDate) &
  months_since(StartDate, HospDate, Months) &
  less(Months, 12)

% Not sure what to do with these exclusions that are about factors external to
% the policy and the hospitalization. Maybe we do need to model claims
% explicitly.
exclusion(af9100, _, 2_b) :- sanctions
exclusion(af9100, _, 2_c) :- fraud

% Section D: Excluded causes

% Routine pregnancy/newborn care in the first 10 months of coverage
exclusion(af9100, Hospitalization, 2_d_1) :-
  eventual_cause_type(Hospitalization, pregnancy_routine) &
  policy.startdate(af9100, PS) &
  evaluate(parsedate(PS), StartDate) &
  hospitalization.startdate(Hospitalization,ZS) &
  evaluate(parsedate(ZS), HospDate) &
  months_since(StartDate, HospDate, Months) &
  less(Months, 10)

excluded_cause(routine_newborn, 2_d_2)
excluded_cause(drugs, 2_d_3)
excluded_cause(felony, 2_d_4)
excluded_cause(intoxicated, 2_d_5)
excluded_cause(self_inflicted, 2_d_6)

exclusion(af9100, Hospitalization, 2_d_7) :-
  cause(Hospitalization, Event) &
  type(Event, dental_care) &
  ~eventual_cause_type(Event, injury)

excluded_cause(surgery_not_necessary, 2_d_8)
excluded_cause(war, 2_d_9)

% Organ donation in the first year of coverage
exclusion(af9100, Hospitalization, 2_d_10) :-
  eventual_cause_type(Hospitalization, organ_donation) &
  policy.startdate(af9100, PS) &
  evaluate(parsedate(PS), StartDate) &
  hospitalization.startdate(Hospitalization,ZS) &
  evaluate(parsedate(ZS), HospDate) &
  months_since(StartDate, HospDate, Months) &
  less(Months, 12)

exclusion(af9100, Hospitalization, 2_d_11) :-
  eventual_cause_type(Hospitalization, mental) &
  ~eventual_cause_type(Hospitalization, senility)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% util
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

eventual_cause_type(Event, Type) :-
  eventual_cause(Event, Cause) &
  type(Cause, Type)